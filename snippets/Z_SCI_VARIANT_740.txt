*&---------------------------------------------------------------------*
*& Report Z_SCI_VARIANT
*&---------------------------------------------------------------------*
*& DSAG ATC Leitfaden
*&---------------------------------------------------------------------*
REPORT Z_SCI_VARIANT.

PARAMETERS p_user TYPE syuname.
PARAMETERS p_vari TYPE sci_chkv DEFAULT 'DEFAULT'.
PARAMETERS p_activ TYPE abap_bool AS CHECKBOX.

DATA: lr_chk_var     TYPE REF TO cl_ci_checkvariant.

* get reference to check variant
cl_ci_checkvariant=>get_ref(
  EXPORTING
    p_user            = p_user
    p_name            = p_vari
  RECEIVING
    p_ref             = lr_chk_var
  EXCEPTIONS
    chkv_not_exists   = 1
    missing_parameter = 2
    OTHERS            = 3 ).
CHECK sy-subrc = 0.

lr_chk_var->get_info(
  EXCEPTIONS
    could_not_read_variant = 1 " Prüfvariante konnte nicht gelesen werden
    OTHERS                 = 2
).
CHECK sy-subrc = 0.

*   TREE
DATA(lo_tests) = cl_ci_tests=>get_tree(
                     p_variant   = lr_chk_var->variant
                     p_transport = lr_chk_var->chkvinf-transport ).
DATA(lo_tree) = lo_tests->tree.

WRITE: / |{ COND #( WHEN p_user IS NOT INITIAL THEN 'Benutzerspezifische' ELSE 'Globale' ) } Prüfvariante { p_vari }|.
ULINE.

DATA lo_check TYPE REF TO cl_ci_test_root.

LOOP AT lo_tree->childs INTO DATA(lr_category).

  IF p_activ = abap_true.
    CHECK lr_category->selected = abap_true.
  ENDIF.

  WRITE: / |[{ lr_category->selected }] { lr_category->description } ({ lr_category->name })|.

  LOOP AT lr_category->childs INTO DATA(lr_check).

    IF p_activ = abap_true.
      CHECK lr_check->selected = abap_true.
    ENDIF.

    TRY.
        " erst ab Rel. >= 7.51 verfügbar	
        lo_check ?= lr_check.
        DATA(l_remote_rfc_enabled) = lo_check->remote_rfc_enabled.
      CATCH cx_sy_move_cast_error.
    ENDTRY.

    WRITE: / |-- [{ lr_check->selected }]| &&
             |{ COND #( WHEN l_remote_rfc_enabled = abap_true THEN '[R]' ELSE ' ' ) } | &&
             |{ lr_check->description } ({ lr_check->name })|.

  ENDLOOP.
  WRITE: /.

ENDLOOP.